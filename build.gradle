apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'

version = "1.0.1"

sourceCompatibility=1.5
targetCompatibility=1.5

repositories {
    mavenCentral()
    maven{
        url "http://repo.springsource.org/release/"
    }
    maven{
        url "http://repo.springsource.org/milestone/"
    }
    maven{
        url "http://files.couchbase.com/maven2/"
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

project.ext {
    mavenGroup = 'com.boissinot.maven.util'
    mavenDescription = 'Maven Mongo Importer'
    mavenScmUrl = 'https://github.com/gboissinot/maven-mongoimporter'
    mavenDeveloperConnectionUrl = 'scm:git:ssh:git@github.com:gboissinot/maven-mongoimporter.git'
    mavenConnectionUrl = 'scm:git:git://github.com/gboissinot/maven-mongoimporter.git'
    mavenStatus = 'RELEASE'
}

mainClassName = "com.boissinot.maven.util.mongoimport.launch.AppMain"

dependencies {
    compile "org.springframework:spring-core:3.2.5.RELEASE"
    compile "org.springframework.integration:spring-integration-core:2.2.6.RELEASE"
    compile "org.springframework.integration:spring-integration-mongodb:2.2.6.RELEASE"
    compile "org.springframework.integration:spring-integration-stream:2.2.6.RELEASE"
    compile "com.couchbase.client:couchbase-client:1.2.2"
    compile "org.springframework.data:spring-data-couchbase:1.0.0.M1"
    compile "com.fasterxml.jackson.core:jackson-core:2.3.0-rc1"
    compile "org.codehaus.jackson:jackson-core-asl:1.9.13"
    compile "org.codehaus.jackson:jackson-mapper-asl:1.9.13"
    compile "org.springframework.data:spring-data-mongodb:1.3.2.RELEASE"
    compile "org.slf4j:slf4j-api:1.7.5"
    compile "org.slf4j:jcl-over-slf4j:1.7.5"
    compile "org.slf4j:slf4j-log4j12:1.7.5"
    compile "org.mongodb:mongo-java-driver:2.11.3"
    compile "org.apache.maven.indexer:indexer-core:5.1.0"
    compile "org.apache.maven.wagon:wagon-http-lightweight:2.2"
    compile "org.sonatype.sisu:sisu-inject-plexus:2.1.1"
    compile "cglib:cglib-nodep:2.2.2"
}



publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId project.mavenGroup
            version version + "." + project.mavenStatus

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }

            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            artifact('README.md') {
                classifier = 'README'
                extension = 'md'
            }

            artifact(distZip){
                classifier= 'distribution'
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description project.mavenDescription
                    scm {
                        url project.mavenScmUrl
                        connection project.mavenConnectionUrl
                        developerConnection project.mavenDeveloperConnectionUrl
                    }
                    licenses {
                        license {
                            name 'MIT license'
                            comments 'All source code is under the MIT license.'
                        }
                    }
                    developers {
                        developer {
                            id 'gboissinot'
                            name 'Gregory Boissinot'
                            email 'gregory.boissinot@gmail.com'
                        }
                    }
                }
            }

            repositories {
                maven {
                    url "file:/${project.projectDir}/artifacts"
                }
            }
        }
    }
}

